***************************
* SET UP THE INITIAL DATA *
***************************

NAME          ARWHDNE

*   Problem :
*   *********
*   A quartic problem whose Hessian is an arrow-head (downwards) with
*   diagonal central part and border-width of 1.
*   Nonlinear equation version of problem ARWHEAD.

*   Source: Problem 55 in
*   A.R. Conn, N.I.M. Gould, M. Lescrenier and Ph.L. Toint,
*   "Performance of a multifrontal scheme for partially separable
*   optimization",
*   Report 88/4, Dept of Mathematics, FUNDP (Namur, B), 1988.

*   SIF input: Ph. Toint, Dec 1989.

*   classification NOR2-AN-V-V

*   N is the number of variables

*IE N                   2              $-PARAMETER
*IE N                   100            $-PARAMETER
 IE N                   500            $-PARAMETER
*IE N                   1000           $-PARAMETER
*IE N                   5000           $-PARAMETER

*   Number of groups sets: each set has a linear and a nonlinear group

 IA NGS       N         -1

*   Define useful parameters

 IE 1                   1

VARIABLES

 DO I         1                        N
 X  X(I)
 ND

GROUPS

 DO I         1                        NGS

*   The linear group of the set

 XE L(I)      X(I)      -4.0

*   The nonlinear group of the set (square)

 XE G(I)

 ND

CONSTANTS

*   Linear groups only

 DO I         1                        NGS
 X  ARWHDNE   L(I)      -3.0
 ND

BOUNDS

 FR ARWHDNE   'DEFAULT'

START POINT

 XV ARWHDNE   'DEFAULT' 1.0

ELEMENT TYPE

 EV SQ        V

ELEMENT USES

 T  'DEFAULT' SQ

 DO I         1                        NGS
 ZV A(I)      V                        X(I)
 ZV B(I)      V                        X(N)
 ND

GROUP USES

*   Include both squared terms in the nonlinear groups

 DO I         1                        NGS

 XE G(I)      A(I)                     B(I)

 ND

OBJECT BOUND

*   Solution

*LO SOLTN               0.0

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      ARWHDNE

INDIVIDUALS

*   Square type element

 T  SQ
 F                      V * V
 G  V                   V + V
 H  V         V         2.0

ENDATA
